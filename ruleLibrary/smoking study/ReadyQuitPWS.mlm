Maintenance:
	Title:		Smoking  Ready to Quit PWS;;
	Filename:		ReadyQuitPWS;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Vivienne Zhu, Steve Downs;;
	Specialist:	Pediatrics;;
	Date:		2012-06-17 11:18:34;;
	Validation:	;;
Library:
	Purpose:		Assistant the smoker who is ready to quit;;
	Explanation:	;;
	Keywords:	Environmental smoking, smoking cessation, parental;;
	Citations:		;;
	Links:		;;
Knowledge:
	Type:		data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) then
VisitType := read Last {VisitType} Where it Occurred Within Past 1 Day;
smokerL := read Last {any_household_members_smoke from CHICA} where it Occurred within Past 18 months;
arm:=read last {smoking_study_arm};
endif
;;
Priority:		508;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) then
If  (VisitType = SickVisit) then conclude false;

If (arm = NULL) then conclude False;
If (arm = Control)  then conclude False;
If (smokerL = yes_ready_to_quit) then conclude True;
conclude False;

endif

If (mode = CONSUME) then
If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then
 CALL storeNote With "Family reported a smoker in child's home is motivated to quit.", "PSYCHOSOCIAL";
endif;

If  (Box1=true) then
 CALL storeObs With "referral","Wishard smoking cessation 630-7653";
 CALL storeNote With "I gave the family the phone number for the Wishard quit smoking program.", "PSYCHOSOCIAL";
endif;
If  (Box2=true) then
 CALL storeObs With "counseling topic","Smoking quit date";
 CALL storeNote With "I recommended setting a quit date.", "PSYCHOSOCIAL";
endif;
If  (Box3=true) then
 CALL storeObs With "JIT hand out given","Smoking cessation advice";
 CALL storeNote With "I provided the CHICA cessation advice handout.", "PSYCHOSOCIAL";
endif;
If  (Box4=true) then
 CALL storeObs With "prescription_for_cessation","Nicotine replacement";
 CALL storeNote With "I recommended nicotine replacement therapy.", "PSYCHOSOCIAL";
endif;
If  (Box5=true) then
 CALL storeObs With "any_household_members_smoke","yes_not_ready_to_quit";
 CALL storeNote With "By history today, smoker is not ready to quit.", "PSYCHOSOCIAL";
endif;
If  (Box6=true) then
 CALL storeObs With "any_household_members_smoke","former";
 CALL storeNote With "By history, there is no longer a smoker in the home.", "PSYCHOSOCIAL";
endif;
If NOT  (Box5=true) then
 CALL storeObs With "any_household_members_smoke","yes_ready_to_quit";
endif;


endif
;;
Action:
call CREATE_JIT with "Smoking_JIT_Ready_To_Quit";
write ("|| firstname || is reportedly exposed to tobacco smoke. The smoker is ready to quit. Please counsel (Give attached handout)");
write ("Wishard smoking cessation 630-7653");
write ("Set quit date");
write ("JIT hand out given");
write ("Nicotine replacement");
write ("Not motivated");
write ("Smoker quit (or left)");
;;
Age_Min: 0 days;;
Age_Max: 11 years;;
end:
