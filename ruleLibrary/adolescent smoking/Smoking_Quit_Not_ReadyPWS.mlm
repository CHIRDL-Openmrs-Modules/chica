Maintenance:
	Title:		Smoking  Not Ready to Quit PWS;;
	Filename:		Smoking_Quit_Not_ReadyPWS;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Aaron E. Carroll;;
	Specialist:	Pediatrics;;
	Date:		2003-10-22T04:17:22-0400;;
	Validation:	;;
Library:
	Purpose:		GAPS recommends different courses of action depending on whether the adolescent is ready to quit using tobacco.  This PWS is for those who are NOTready to quit.                                                                                                                                                                                                                                                                                                                                                    ;;
	Explanation:	More than 4.5 million adolescents use tobacco regularly.  Adolescence is a key time to prevent tobacco use and try and change behavior.                                                                                                                                                                                                                                                                                                                                                                             ;;
	Keywords:	GAPS, adolescence, tobacco, smoking, PWS;;
	Citations:		;;
	Links:		;;
Knowledge:
	Type:		data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) then
VisitType := read Last {VisitType} Where it Occurred Within Past 1 Day;
use := read Last {ever_use_tobacco from CHICA} ;
ready := read Last {ready_to_quit from CHICA} ;
readyX := read Exist {ready_to_quit from CHICA};                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                           
endif
;;
Priority:		448;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) then
If  (VisitType = SickVisit) then conclude false;
If (use = "True") AND NOT (ready = "True") then conclude True;
If (use = "True") AND (readyX = False) then conclude True;
conclude False;                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    

endif

If (mode = CONSUME) then

/* This section defines the variables used when calling the storeNote function. */
If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then || description || = "|| firstname || admits to smoking, and indicates NO readiness to quit. I further assessed tobacco use and counseled the patient about tobacco cessation.";
If  (Box1=true) then || note1 || = "I expressed concern about tobacco use harming || firstname ||'s health.";
If  (Box6=true) then || note6 || = "|| firstname || is ready to quit using tobacco today.";
/* */

If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then
 CALL storeNote With "description","PSYCHOSOCIAL";
endif;

If  (Box1=true) then
 CALL storeNote With "note1","PSYCHOSOCIAL";
endif;
If  (Box2=true) then
 CALL storeNote With "I reviewed reasons to avoid tobacco use, emphasizing health consequences.","PSYCHOSOCIAL";
endif;
If  (Box3=true) then
 CALL storeNote With "I discussed the value of tobacco cessation.","PSYCHOSOCIAL";
endif;
If  (Box4=true) then
 CALL storeNote With "I provided information about 1-800-QUIT-NOW and other resources for tobacco cessation.","PSYCHOSOCIAL";
endif;
If  (Box5=true) then
 CALL storeNote With "I provided handouts for tobacco cessation.","PSYCHOSOCIAL";
endif;
If  (Box6=true) then
 CALL storeObs With "ever_use_tobacco","False";
 CALL storeNote With "note6","PSYCHOSOCIAL";
endif;


endif
;;
Action:
write ("|| firstname || admits to smoking, and indicates NO readiness to quit. Explore further details about tobacco use including quantity, frequency, and type.  Assess impact on quality of life, and past attempts to quit. Consider the following actions:");                                          
write ("Express concern about behavior");
write ("Discuss health consequences");
write ("Discuss value of quitting");
write ("Provide hotline contacts");
write ("Hand out materials");
write ("Adolescent has quit!");
;;
Age_Min: 11 years;;
Age_Max: 21 years;;
end:
