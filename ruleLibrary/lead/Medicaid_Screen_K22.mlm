Maintenance:
	Title:		MedicaidLeadRisk;;
	Filename:		Medicaid_Screen_K22;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Steve Downs;;
	Specialist:	Pediatrics;;
	Date:		2008-02-01T05:18:21-0500;;
	Validation:	;;
Library:
	Purpose:		Remind MD to obtain blood lead on medicaid patient;;
	Explanation:	If patient between ages 1 and 3 is on Medicaid and has not had a blood lead drawn in over a year, PWS prompt to get a blood lead;;
	Keywords:	Lead, Medicaid, screening, well child care, PWS;;
	Citations:		CPAC guideline;;
	Links:		;;
Knowledge:
	Type:		data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) then
VisitType := read Last {VisitType} Where it Occurred Within Past 1 Day;
Last_Pb := read Exist {LEAD LEVEL-PEDS from RMRS} Where it Occurred Within Past 360 Days;
Qual_Pb := read Exist {VENOUS BLOOD LEAD - QUALITATIVE from CHICA} Where it Occurred Within Past 360 Days;
Insurance := read Last {Insurance from CHICA};
endif
;;
Priority:		740;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) then
If  (VisitType = SickVisit) then conclude false;

If (Last_Pb = True) then conclude False;
If (Qual_Pb = True) then conclude False;
If (Insurance = Medicaid) then conclude True;

endif

If (mode = CONSUME) then
If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then
  CALL storeNote With "Because of Medicaid eligibility, child is considered high risk for lead intoxication.", "SCREENING";
endif;

If  (Box1=true) then
  CALL storeNote With "A blood lead has been obtained this year.", "SCREENING";
endif;
If  (Box2=true) then
 CALL storeObs With "VENOUS BLOOD LEAD - QUALITATIVE","10 - 14 MG/DL";
 CALL storeNote With "Previous blood lead was 10-14 mg/dl. Child needs a CBC and repeat blood lead in 3 mos.", "SCREENING";
endif;
If  (Box3=true) then
 CALL storeObs With "VENOUS BLOOD LEAD - QUALITATIVE","LESS THAN 10 MG/DL";
 CALL storeNote With "Previous blood lead was <10 mg/dl - OK.", "SCREENING";
endif;
If  (Box4=true) then
 CALL storeObs With "VENOUS BLOOD LEAD - QUALITATIVE","15 - 19 MG/DL";
 CALL storeNote With "Previous blood lead was 15-19 mg/dl. Child needs a CBC and repeat blood lead in 3 mos. Will notify health dept.", "SCREENING";
endif;
If  (Box5=true) then
 CALL storeObs With "VENOUS BLOOD LEAD - QUALITATIVE","PENDING";
 CALL storeNote With "Blood lead ordered today.", "SCREENING";
endif;
If  (Box6=true) then
 CALL storeObs With "VENOUS BLOOD LEAD - QUALITATIVE","GREATER THAN 19 MG/DL";
 CALL storeNote With "Previous blood lead was >19.  Will obtain CBC and contact health dept and toxicology to initiate acute treatment.", "SCREENING";
endif;


endif
;;
Action:
write("Children this age who are eligible for Medicaid are considered by the CDC to be at risk for lead intoxication.  A blood lead level is recommended annually.");
write ("Blood Pb done this year----->");
write ("10-14 mg/dl: CBC+ Pb 3mo");
write ("Previous blood lead <10 (OK)");
write ("15-19: above+call hlth dept");
write ("Blood lead ordered today");
write (">19: above+call toxicolgy");
;;
Age_Min: 10 months;;
Age_Max: 36 months;;
end:
