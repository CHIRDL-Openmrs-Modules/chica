Maintenance:
	Title:		High Blood Pressure in Hypertensive PWS;;
	Filename:	BP_Hi_Htn;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Steve Downs;;
	Specialist:	Pediatrics;;
	Date:		2004-09-02 12:44:49;;
	Validation:	;;
Library:
	Purpose:	Alert MD if BP is high in a patient with hypertensive.;;
	Explanation:	Uses BP_Eval variable to track response to elevated BP.  If elevated BP not previously documented, asks for repeat;;
	Keywords:	blood pressure BP hypertension;;
	Citations:	;;
	Links:	;;
Knowledge:
	Type:	data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) then
patientsys := read Last {Systolic_BP from CHICA} Where it Occurred Within Past 365 Days;
patientdias := read Last {Diastolic_BP from CHICA} Where it Occurred Within Past 365 Days;
patienthtcentile := read Last {HtCentile from CHICA} Where it Occurred Within Past 1 Days;
BP_status := read Last {BP_Eval from CHICA} Where it Occurred Within Past 4 Years;
endif
;;
Priority:		219;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) then
sys90bp := call LookupBPcentile with "Systolic", "90", "patienthtcentile";
dias90bp := call LookupBPcentile with "Diastolic", "90", "patienthtcentile";

If NOT (BP_status = hypertensive) then conclude False;
If (patientsys >= sys90bp) OR (patientdias >= dias90bp) then conclude True;

endif

If (mode = CONSUME) then
If  (Box1=true) OR (Box2=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then || description || = "|| firstname || has been hypertensive in the past and BP remains above the 90th %tile.";
If  (Box1=true) OR (Box2=true) OR (Box4=true) OR (Box5=true) OR (Box6=true) then
 CALL storeNote With "description", "SCREENING";
endif;

If  (Box1=true) then
 CALL storeObs With "BP_Eval","hypertensive";
 CALL storeNote With "I rechecked BP, and it is still elevated", "SCREENING";
endif;
If  (Box2=true) then
 CALL storeNote With "I reinforced giving a low sodium diet.", "SCREENING";
endif;
If  (Box5=true) then
 CALL storeObs With "BP_Eval","repeat_normal";
 CALL storeNote With "I repeated the BP, and it was normal.", "SCREENING";
endif;
If  (Box4=true) then
 CALL storeNote With "I made a nephrology referral.", "SCREENING";
endif;
If  (Box6=true) then
 CALL storeNote With "I started the patient on lisinopril.", "SCREENING";
endif;


endif
;;
Action:
write ("|| firstname || has been hypertensive and || hisher || BP, || patientsys_value ||/|| patientdias_value || on || patientsys_date ||, remains above the 90th %tile (|| sys90bp_value || / || dias90bp_value ||). Recheck now and consider:");
write ("Recheck BP is elevated ---->");
write ("Reinforce Low Na diet");
write ("");
write ("Nephrol referral");
write ("Recheck BP is normal");
write ("Lisinopril");
;;
Age_Min: 5 years;;
Age_Max: 18 years;;
end:
