Maintenance:
	Title:		Blood Pressure Positive Screen PWS;;
	Filename:	BP_Pos_Screen;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Steve Downs;;
	Specialist:	Pediatrics;;
	Date:		2004-09-02 09:49:33;;
	Validation:	;;
Library:
	Purpose:	Prompt MD to follow up a first time elevated blood pressure.;;
	Explanation:	Uses BP_Eval variable to track response to elevated BP.  If elevated BP not previously documented, asks for repeat;;
	Keywords:	blood pressure BP hypertension;;
	Citations:	;;
	Links:	;;
Knowledge:
	Type:	data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) then
VisitType := read Last {VisitType} Where it Occurred Within Past 1 Day;
patientsys := read Last {Systolic_BP from CHICA} Where it Occurred Within Past 365 Days;
patientdias := read Last {Diastolic_BP from CHICA} Where it Occurred Within Past 365 Days;
patienthtcentile := read Last {HtCentile from CHICA} Where it Occurred Within Past 1 Days;
BP_status := read Last {BP_Eval from CHICA} Where it Occurred Within Past 4 Years;
endif
;;
Priority:		221;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) then
If  (VisitType = SickVisit) then conclude false;

sys90bp := call LookupBPcentile with "Systolic", "90", "patienthtcentile";
dias90bp := call LookupBPcentile with "Diastolic", "90", "patienthtcentile";

If (BP_status = elevated_once) then conclude False;
If (BP_status = hypertensive) then conclude False;
If (patientsys >= sys90bp) OR (patientdias >= dias90bp) then conclude True;

endif

If (mode = CONSUME) then
If  (Box1=true) OR (Box2=true) OR (Box3=true) then || description || = "|| firstname ||'s had an elevated blood pressure.";
If  (Box1=true) OR (Box2=true) OR (Box3=true) then
 CALL storeNote With "description", "SCREENING";
endif;

If  (Box1=true) then
 CALL storeObs With "BP_Eval","elevated_once";
 CALL storeNote With "I repeated it, and it was still high.", "SCREENING";
endif;
If  (Box2=true) then
 CALL storeNote With "Patient will return for a repeat blood pressure measurement in 2-3 weeks.", "SCREENING";
endif;
If  (Box3=true) then
 CALL storeObs With "BP_Eval","recheck_normal";
 CALL storeNote With "I repeated it, and it was normal.", "SCREENING";
endif;


endif
;;
Action:
write ("|| firstname ||'s BP, || patientsys_value ||/|| patientdias_value || on || patientsys_date || is above the 90th %tile (|| sys90bp_value ||/|| dias90bp_value ||).  Recheck it now.  If elevated, recommend a followup BP in 2-3 wks.");
write ("Recheck BP is elevated ---->");
write ("Followup in 2-3 wks arranged");
write ("Recheck BP is normal");
write ("");
write ("");
write ("");
;;
Age_Min: 5 years;;
Age_Max: 18 years;;
end:
