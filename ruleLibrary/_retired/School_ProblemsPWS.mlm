Maintenance:
	Title:		School and Learning Problems  PWS;;
	Filename:		School_ProblemsPWS;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Aaron E. Carroll;;
	Specialist:	Pediatrics;;
	Date:		2003-10-22T01:38:18-0400;;
	Validation:	;;
Library:
	Purpose:		This PWS will fire if any of the screening questions are answered in a concerning manner.                                                                                                                                                                                                                                                                                                                                                                                                                           ;;
	Explanation:	School performance is one of the most sensitive markers of psychosocial adjustment to adolescence.  Assessing this is not easy or simple.  This PSF asks a simple screening question from the GAPS handbook.                                                                                                                                                                                                                                                                                                        ;;
	Keywords:	adolescence, GAPS, school,  PWS;;
	Citations:		;;
	Links:		http://www.ama-assn.org/ama/pub/category/2280.html;;
Knowledge:
	Type:		data_driven;;
Data:

mode:=read {mode from Parameters};
Box1:=read {box1 from Parameters};
Box2:=read {box2 from Parameters};
Box3:=read {box3 from Parameters};
Box4:=read {box4 from Parameters};
Box5:=read {box5 from Parameters};
Box6:=read {box6 from Parameters};

If (mode = PRODUCE) OR (mode = CONSUME) then
grades := read Last {grades_dropped_lately from CHICA} Where it Occurred Within Past 365 Days;
special := read Last {special_education_classes from CHICA} Where it Occurred Within Past 365 Days;
suspend := read Last {school_suspension_this_year from CHICA} Where it Occurred Within Past 365 Days;
endif

If (mode = PRODUCE) then
VisitType := read Last {VisitType} Where it Occurred Within Past 1 Day;
pastProblems := read Last {past_school_problems from CHICA} ;                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              
endif
;;
Priority:		1000;;
	Evoke:		;;
Logic:
If (mode = PRODUCE) OR (mode = CONSUME) then
If (grades = "True") then || gradesV || = "GRADES WORSE THIS YEAR, ";
If (special = "True") then || specialV || = "IN SPECIAL EDUCATION CLASSES, ";
If (suspend = "True") then || suspendlV || = "SUSPENDED FROM SCHOOL";	
endif

If (mode = PRODUCE) then
If  (VisitType = SickVisit) then conclude false;
If (Gender = M) then || heshe || = "he";
If (Gender = F) then || heshe || = "she";
If (pastProblems = "True") then conclude False;
If (grades = "True") OR (special = "True") OR (suspend = "True") then conclude True;
conclude False;                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               

endif

If (mode = CONSUME) then

/* This section defines the variables used when calling the storeNote function. */
If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box6=true) then || description || = "|| firstname || has risk factors for school/learning problems: || gradesV |||| specialV |||| suspendlV ||. I assessed the patient for other risk factors, causes of school problems, and strategies to manage problems.";
If  (Box1=true) then || note1 || = "I am not concerned about || firstname || being at risk for school/learning problems.";
If  (Box2=true) then || note2 || = "I referred || firstname || for educational testing (e.g. IQ test, assessment for learning disability).";
If  (Box3=true) then || note3 || = "I am concerned about || firstname || having school/learning problems.";
If  (Box4=true) then || note4 || = "I referred || firstname || for developmental or psychological testing.";
/* */

If  (Box1=true) OR (Box2=true) OR (Box3=true) OR (Box4=true) OR (Box6=true) then
 CALL storeNote With "description","CHICA NOTES";
endif;

If  (Box1=true) then
 CALL storeObs With "past_school_problems","False";
 CALL storeNote With "note1","CHICA NOTES";
endif;
If  (Box3=true) then
 CALL storeObs With "past_school_problems","True";
 CALL storeNote With "note3","CHICA NOTES";
endif;
If  (Box2=true) then
 CALL storeObs With "past_school_problems","True";
 CALL storeNote With "note2","CHICA NOTES";
endif;
If  (Box4=true) then
 CALL storeObs With "past_school_problems","True";
 CALL storeNote With "note4","CHICA NOTES";
endif;
If  (Box6=true) then
 CALL storeObs With "past_school_problems","True";
 CALL storeNote With "I worked with the parents to create a management plan for school/learning problems.","CHICA NOTES";
endif;


endif
;;
Action:
write ("|| firstname || has risk factors for school/learning problems: || gradesV |||| specialV |||| suspendlV ||. Ask about school, future plans, & how || heshe || is managing issues. Assess further risk factors.  Explore causes (family problems, substance abuse, health issues, etc.)");            
write ("No school or learning problems");
write ("Refer for Psych-Ed test");
write ("School/learning problem ->");
write ("Get develop/psych assessments");
write ("");
write ("Created management plan");
;;
Age_Min: 12 years;;
Age_Max: 21 years;;
end:
